plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    namespace 'com.karrar.movieapp'
    compileSdk 34

    defaultConfig {
        applicationId "com.karrar.movieapp"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_KEY", "\"${localProperties.getProperty('apiKey', '')}\""
        buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "IMAGE_BASE_PATH", "\"https://image.tmdb.org/t/p/w500\""
        buildConfigField "String", "TMDB_SIGNUP_URL", "\"https://www.themoviedb.org/signup\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnitPlatformBuilder"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig true
        dataBinding true
        viewBinding true
    }

}

dependencies {
    // Core Android
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Architecture Components
    implementation 'androidx.fragment:fragment-ktx:1.8.2'
    implementation 'androidx.activity:activity-ktx:1.9.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.4'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.2"

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.12.0"

    // Image Loading
    implementation 'io.coil-kt:coil:2.7.0'

    // Room Database
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Hilt (Dependency Injection)
    implementation "com.google.dagger:hilt-android:2.52"
    kapt "com.google.dagger:hilt-compiler:2.52"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.3.2"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.1.1"

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Lottie Animations
    implementation 'com.airbnb.android:lottie:6.4.1'

    // YouTube Player
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:12.1.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.2"
    testImplementation "io.mockk:mockk:1.13.10"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0"

    // Android Testing
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "io.mockk:mockk-android:1.13.10"

    implementation 'io.github.glailton.expandabletextview:expandabletextview:1.0.2'
}